# Maintainer: Sean Liao <mono-pkgbuild@seankhliao.com>

_pkgname=ghdefaults
pkgname=${_pkgname}-git
pkgver=r109.e34f7fb
pkgrel=1
pkgdesc="ghdefaults web server"
arch=(x86_64)
url="https://github.com/seankhliao/mono"
license=(MIT)
depends=(age)
makedepends=(git go)
provides=(${_pkgname})
conflicts=(${_pkgname})
install=hooks.install
source=("src::git+${url}"
    ${_pkgname}.service
    private-key.age
    webhook-secret.age)
sha256sums=('SKIP'
            'dd986b70e8ef417772bbe5b2103a96ede549eaac36efc38b80e9e4a2e4d3901d'
            '73502e2979fd13e5f5c2600a0c2c5b61562e2cca97a3ed75b96d41913ea0db86'
            '6a4ee5aad1cd6cfa5b4ab095450dee233734520ac6181f1cc2d83d531bd27460')

build() {
  cd src
  export CGO_ENABLED=0
  export GOFLAGS='-trimpath -mod=readonly -modcacherw'

  go build -ldflags='-w -s' -o ./bin/${_pkgname} ./go/cmd/${_pkgname}
}

package() {
  install -Dm755 src/bin/${_pkgname} ${pkgdir}/usr/bin/${_pkgname}
  install -Dm644 src/LICENSE ${pkgdir}/usr/share/licenses/${_pkgname}/LICENSE
  install -Dm644 ${_pkgname}.service ${pkgdir}/usr/lib/systemd/system/${_pkgname}.service
  install -Dm644 private-key.age ${pkgdir}/etc/${_pkgname}/private-key.age
  install -Dm644 webhook-secret.age ${pkgdir}/etc/${_pkgname}/webhook-secret.age
}

pkgver() {
    cd src
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}
