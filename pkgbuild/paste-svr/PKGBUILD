# Maintainer: Sean Liao <mono-pkgbuild@seankhliao.com>

_pkgname=paste-svr
pkgname=${_pkgname}-git
pkgver=r110.06802f8
pkgrel=1
pkgdesc="paste web server"
arch=(x86_64)
url="https://github.com/seankhliao/mono"
license=(MIT)
makedepends=(git go)
provides=(${_pkgname})
conflicts=(${_pkgname})
source=("src::git+${url}"
    ${_pkgname}.service)
sha256sums=('SKIP'
            '8a400f51d62b22f72ebd9dc4b1dd5a0f5444995558187a6a0976d5ba58aa987f')

build() {
  cd src
  export CGO_ENABLED=0
  export GOFLAGS='-trimpath -mod=readonly -modcacherw'

  go build -ldflags='-w -s' -o ./bin/${_pkgname} ./go/cmd/paste
}

package() {
  install -Dm755 src/bin/${_pkgname} ${pkgdir}/usr/bin/${_pkgname}
  install -Dm644 src/LICENSE ${pkgdir}/usr/share/licenses/${_pkgname}/LICENSE
  install -Dm644 ${_pkgname}.service ${pkgdir}/usr/lib/systemd/system/${_pkgname}.service
}

pkgver() {
    cd src
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}
