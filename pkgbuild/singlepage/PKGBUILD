# Maintainer: Sean Liao <mono-pkgbuild@seankhliao.com>

_pkgname=singlepage
pkgname=${_pkgname}-git
pkgver=r111.e27f817
pkgrel=1
pkgdesc="singlepage web server"
arch=(x86_64)
url="https://github.com/seankhliao/mono"
license=(MIT)
makedepends=(git go)
provides=(${_pkgname})
conflicts=(${_pkgname})
source=("src::git+${url}"
    ${_pkgname}@.service)
sha256sums=('SKIP'
            'a4107fd400785e92c63ec5aa8fbd1fe8dbe846d22a6be517396e080519f1e387')

build() {
  cd src
  export CGO_ENABLED=0
  export GOFLAGS='-trimpath -mod=readonly -modcacherw'

  go build -ldflags='-w -s' -o ./bin/${_pkgname} ./go/cmd/${_pkgname}
}

package() {
  install -Dm755 src/bin/${_pkgname} ${pkgdir}/usr/bin/${_pkgname}
  install -Dm644 src/LICENSE ${pkgdir}/usr/share/licenses/${_pkgname}/LICENSE
  install -Dm644 ${_pkgname}@.service ${pkgdir}/usr/lib/systemd/system/${_pkgname}@.service
}

pkgver() {
    cd src
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}
